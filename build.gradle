plugins {
    id "java"
    id "checkstyle"
    id "maven"
    id "com.github.jruby-gradle.base" version "1.7.0"
    id "org.embulk.embulk-plugins" version "0.2.3"
    id "com.jfrog.bintray" version "1.1"
}

repositories {
    mavenCentral()
    jcenter()
    // TODO(dmikurube): Remove this after embulk-base-restclient goes to jcenter.
    maven {
        url "http://dl.bintray.com/embulk-base-restclient/maven"
    }
}

group = "io.github.dmikurube"
version = "0.2.5"
description = "Loads records from Yelp."

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-core:0.9.17"
    compile("org.embulk.base.restclient:embulk-base-restclient:0.5.0") {
        exclude group: "org.embulk", module: "embulk-core"
    }
    compile("org.embulk.base.restclient:embulk-util-retryhelper-jaxrs:0.5.0") {
        exclude group: "org.embulk", module: "embulk-core"
    }
    compile("org.glassfish.jersey.core:jersey-client:2.25.1") {
        exclude group: "javax.inject", module: "javax.inject"
    }
    testCompile "junit:junit:4.+"
}

embulkPlugin {
    mainClass = "org.embulk.input.yelp.YelpInputPlugin"
    category = "input"
    type = "yelp"
}

gem {
    authors = [ "Dai MIKURUBE" ]
    email = [ "dmikurube@acm.org" ]
    summary = "Yelp input plugin for Embulk"
    homepage = "https://github.com/dmikurube/embulk-input-yelp"
    licenses = [ "MIT" ]
}

gemPush {
    host = "https://rubygems.org"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}
